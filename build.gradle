/*
 * Copyright 2015 codecentric AG
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    group 'de.codecentric.gradle'
    version = '0.0.1-SNAPSHOT'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    compileJava.options.debugOptions.debugLevel = "source,lines,vars"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    idea {
        module {
            jdkName = '1.7'
        }
    }
}

subprojects {
    sourceSets {
        integrationTest {
            java {
                srcDir 'src/test-integration/java'
            }
            resources {
                srcDir 'src/test-integration/resources'
            }
            groovy {
                srcDir 'src/test-integration/groovy'
            }
        }
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile 'com.cyrusinnovation:mockito-groovy-support:1.3'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'org.hamcrest:hamcrest-all:1.3'

        integrationTestCompile sourceSets.main.output
        integrationTestCompile sourceSets.test.output

        integrationTestCompile configurations.compile
        integrationTestCompile configurations.testCompile
        integrationTestRuntime configurations.testRuntime
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
    }
}